/*
 * Copyright (C) 2016  (See AUTHORS)
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'idea'

project.group = 'de.tum.in'
project.version = '0.2.0'

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

repositories {
    maven { url 'https://www7.in.tum.de/~sickert/maven/' }
    mavenCentral()
}

dependencies {
    compileOnly 'com.google.code.findbugs:jsr305:3.0.1'
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'
    compileOnly 'org.immutables:value:2.3.9'

    compile 'com.google.guava:guava:20.0'
    compile 'it.unimi.dsi:fastutil:7.0.13'
    compile files('lib/jhoafparser-1.1.1.jar')

    testCompile 'junit:junit:4+'
    testCompile 'org.hamcrest:hamcrest-all:1.3'

    // Additional Checks
    // findbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:6.6.3'
}


idea {
    module {
        sourceDirs += file('src/main/generated')
        testSourceDirs += file('src/test/generated')
        //generatedSourceDirs += file('src/main/generated')

        jdkName = '1.8'

        downloadJavadoc = true
        downloadSources = true
    }
}


// https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.FindBugs.html
findbugs {
    toolVersion = "3.0.1"
    reportsDir = file("$project.buildDir/reports/findbugs")
    effort = "max"
    reportLevel = "low"
}

tasks.withType(FindBugs) {
    // Findbugs doesn't respect include for some reason ...
    classes = classes.filter {
        it.path.contains('/owl/')
    }
    reports {
        xml.enabled false
        html.enabled true
    }
}

// https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.Pmd.html
pmd {
    toolVersion = "5.5.1"
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("$project.rootDir/config/pmd-rules.xml")
    ruleSets = [] // We specify all rules in rules.xml
    consoleOutput = false // Don't litter console
}

tasks.withType(Pmd) {
    include '**/owl/**'
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

// https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.Checkstyle.html
checkstyle {
    toolVersion = "7.1.2"
    configFile = file("$project.rootDir/config/checkstyle.xml")
    showViolations = false // Don't litter console
    checkstyleMain {
        configProperties = ['suppression-file':
                                    "$project.rootDir/config/checkstyle-main-suppression.xml"]
    }
    checkstyleTest {
        configProperties = ['suppression-file':
                                    "$project.rootDir/config/checkstyle-test-suppression.xml"]
    }
}

tasks.withType(Checkstyle) {
    include '**/owl/**'
}

// If a user file is present, include it.
if (file('../user.gradle').exists()) {
    apply from: '../user.gradle'
}
